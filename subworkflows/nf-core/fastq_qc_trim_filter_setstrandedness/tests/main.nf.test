
nextflow_workflow {

    name "Test Subworkflow FASTQ_QC_TRIM_FILTER_SETSTRANDEDNESS"
    script "../main.nf"
    workflow "FASTQ_QC_TRIM_FILTER_SETSTRANDEDNESS"
    config "./nextflow.config"

    tag "subworkflows"
    tag "subworkflows_nfcore"
    tag "subworkflows/fastq_qc_trim_filter_setstrandedness"

    tag "bbmap/bbsplit"
    tag "cat"
    tag "cat/fastq"
    tag "fastqc"
    tag "fq/lint"
    tag "sortmerna"
    tag "ribodetector"
    tag "seqkit/stats"
    tag "subworkflows/fastq_fastqc_umitools_trimgalore"
    tag "subworkflows/fastq_fastqc_umitools_fastp"
    tag "subworkflows/fastq_subsample_fq_salmon"

    // Global setup: Inject synthetic rRNA reads into test data using CAT_FASTQ
    // The rRNA reads (from generic/fastq/) are 5.8S rRNA sequences that will be detected
    // by both SortMeRNA (alignment-based, 100% detection) and RiboDetector (ML-based, ~70% detection)
    setup {
        run("CAT_FASTQ") {
            script "../../../../modules/nf-core/cat/fastq/main.nf"
            process {
                """
                // CAT_FASTQ input: alternating R1/R2 files [r1_a, r2_a, r1_b, r2_b, ...]
                // Even indices -> R1 files, odd indices -> R2 files
                input[0] = Channel.of([
                    [ id:'test', single_end:false, strandedness:'auto' ],
                    [
                        file(params.modules_testdata_base_path + 'genomics/homo_sapiens/illumina/fastq/test_rnaseq_1.fastq.gz', checkIfExists: true),
                        file(params.modules_testdata_base_path + 'genomics/homo_sapiens/illumina/fastq/test_rnaseq_2.fastq.gz', checkIfExists: true),
                        file(params.modules_testdata_base_path + 'generic/fastq/rrna_reads_1.fastq.gz', checkIfExists: true),
                        file(params.modules_testdata_base_path + 'generic/fastq/rrna_reads_2.fastq.gz', checkIfExists: true)
                    ]
                ])
                """
            }
        }
    }

    test("homo_sapiens paired-end [fastq] fastp sortmerna") {

        when {
            workflow {
                """
                // Input channels
                input[0] = CAT_FASTQ.out.reads
                input[1] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.fasta', checkIfExists: true)) // ch_fasta
                input[2] = Channel.of(file(params.modules_testdata_base_path + "genomics/homo_sapiens/genome/transcriptome.fasta", checkIfExists: true)) // ch_transcript_fasta
                input[3] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.gtf', checkIfExists: true)) // ch_gtf
                input[4] = []              // ch_salmon_index
                input[5] = []              // ch_sortmerna_index
                input[6] = []              // ch_bbsplit_index
                input[7] = Channel.of(file('https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5.8s-database-id98.fasta', checkIfExists: true)) // ch_rrna_fastas

                // Skip options
                input[8] = true            // skip_bbsplit
                input[9] = false           // skip_fastqc
                input[10] = false          // skip_trimming
                input[11] = true           // skip_umi_extract
                input[12] = false          // skip_linting

                // Index generation
                input[13] = true           // make_salmon_index
                input[14] = true           // make_sortmerna_index

                // Trimming options
                input[15] = 'fastp'        // trimmer
                input[16] = 10             // min_trimmed_reads
                input[17] = true           // save_trimmed
                input[18] = true           // fastp_merge

                // rRNA removal options
                input[19] = true           // remove_ribo_rna
                input[20] = 'sortmerna'    // ribo_removal_tool

                // UMI options
                input[21] = false          // with_umi
                input[22] = 0              // umi_discard_read

                // Strandedness thresholds
                input[23] = 0.8            // stranded_threshold
                input[24] = 0.1            // unstranded_threshold
                """
            }
        }

        then {
            def pelines1 = path(workflow.out.reads[0][1][0]).linesGzip
            def pelines2 = path(workflow.out.reads[0][1][1]).linesGzip

            // First part of each fq lint report line is a timestamp, remove it before snapshotting
            def processed_ribo_removal_lint_report = path(workflow.out.lint_log.find { entry -> entry[1].contains('fq_lint_after_ribo_removal')}?.getAt(1))
                .getText()
                .readLines()
                .collect { line -> line.split(' ', 2)[1] } // Split by the first space and take everything after it
                .join('\n') // Join the processed lines back into a single text block

            // Parse sortmerna log for rRNA detection stats
            def sortmernaLog = path(workflow.out.multiqc_files.find { it.toString().endsWith('.sortmerna.log') })
                .getText()
            def sortmernaRrnaMatch = (sortmernaLog =~ /Total reads passing E-value threshold = (\d+)/)
            def sortmernaRrnaCount = sortmernaRrnaMatch ? sortmernaRrnaMatch[0][1].toInteger() : -1

            // Input: 4159 original + 10 synthetic rRNA = 4169 read pairs
            // After fastp merge: 1137 pairs (3022 merged reads)
            // SortMeRNA removes all 10 synthetic rRNA pairs (20 individual reads), leaving 1127 pairs
            assertAll(
                { assert workflow.success },
                { assert pelines1.size() == 4508 },  // 1127 pairs × 4 lines/read
                { assert pelines2.size() == 4508 },
                { assert workflow.out.trim_read_count[0][1] == 3022 },
                { assert sortmernaRrnaCount == 20 },  // 10 pairs = 20 individual reads (100% detection)
                { assert snapshot(
                    pelines1.join('\n').md5(),
                    pelines2.join('\n').md5(),
                    processed_ribo_removal_lint_report
                ).match() }
            )
        }
    }

    test("homo_sapiens paired-end [fastq] fastp ribodetector") {

        tag 'foo'

        when {
            workflow {
                """
                // Input channels
                input[0] = CAT_FASTQ.out.reads
                input[1] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.fasta', checkIfExists: true)) // ch_fasta
                input[2] = Channel.of(file(params.modules_testdata_base_path + "genomics/homo_sapiens/genome/transcriptome.fasta", checkIfExists: true)) // ch_transcript_fasta
                input[3] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.gtf', checkIfExists: true)) // ch_gtf
                input[4] = []              // ch_salmon_index
                input[5] = []              // ch_sortmerna_index
                input[6] = []              // ch_bbsplit_index
                input[7] = []              // ch_rrna_fastas (not needed for ribodetector)

                // Skip options
                input[8] = true            // skip_bbsplit
                input[9] = false           // skip_fastqc
                input[10] = false          // skip_trimming
                input[11] = true           // skip_umi_extract
                input[12] = false          // skip_linting

                // Index generation
                input[13] = true           // make_salmon_index
                input[14] = false          // make_sortmerna_index (not needed for ribodetector)

                // Trimming options
                input[15] = 'fastp'        // trimmer
                input[16] = 10             // min_trimmed_reads
                input[17] = true           // save_trimmed
                input[18] = true           // fastp_merge

                // rRNA removal options
                input[19] = true           // remove_ribo_rna
                input[20] = 'ribodetector' // ribo_removal_tool

                // UMI options
                input[21] = false          // with_umi
                input[22] = 0              // umi_discard_read

                // Strandedness thresholds
                input[23] = 0.8            // stranded_threshold
                input[24] = 0.1            // unstranded_threshold
                """
            }
        }

        then {
            def pelines1 = path(workflow.out.reads[0][1][0]).linesGzip
            def pelines2 = path(workflow.out.reads[0][1][1]).linesGzip

            // First part of each fq lint report line is a timestamp, remove it before snapshotting
            def processed_ribo_removal_lint_report = path(workflow.out.lint_log.find { entry -> entry[1].contains('fq_lint_after_ribo_removal')}?.getAt(1))
                .getText()
                .readLines()
                .collect { line -> line.split(' ', 2)[1] } // Split by the first space and take everything after it
                .join('\n') // Join the processed lines back into a single text block

            // Parse ribodetector log for rRNA detection stats
            // Note: ribodetector log contains ANSI color codes that must be stripped before regex matching
            def ribodetectorLog = path(workflow.out.multiqc_files.find { it.toString().endsWith('.ribodetector.log') })
                .getText()
                .replaceAll(/\u001b\[[0-9;]*m/, '')  // Strip ANSI escape codes
            def ribodetectorRrnaMatch = (ribodetectorLog =~ /Detected (\d+) rRNA sequences/)
            def ribodetectorRrnaCount = ribodetectorRrnaMatch ? ribodetectorRrnaMatch[0][1].toInteger() : -1

            // Input: 4159 original + 10 synthetic rRNA = 4169 read pairs
            // After fastp merge: 1137 pairs (3022 merged reads)
            // RiboDetector removes 7 of 10 synthetic rRNA pairs (70% detection), leaving 1130 pairs
            assertAll(
                { assert workflow.success },
                { assert pelines1.size() == 4520 },  // 1130 pairs × 4 lines/read
                { assert pelines2.size() == 4520 },
                { assert workflow.out.trim_read_count[0][1] == 3022 },
                { assert ribodetectorRrnaCount == 7 },  // 7 pairs detected (70% - ML model misses some)
                { assert snapshot(
                    pelines1.join('\n').md5(),
                    pelines2.join('\n').md5(),
                    processed_ribo_removal_lint_report.md5()
                ).match() }
            )
        }
    }

    test("homo_sapiens paired-end [fastq] trimgalore sortmerna") {

        when {
            workflow {
                """
                // Input channels
                input[0] = CAT_FASTQ.out.reads
                input[1] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.fasta', checkIfExists: true)) // ch_fasta
                input[2] = Channel.of(file(params.modules_testdata_base_path + "genomics/homo_sapiens/genome/transcriptome.fasta", checkIfExists: true)) // ch_transcript_fasta
                input[3] = Channel.of(file(params.modules_testdata_base_path + 'genomics/homo_sapiens/genome/genome.gtf', checkIfExists: true)) // ch_gtf
                input[4] = []              // ch_salmon_index
                input[5] = []              // ch_sortmerna_index
                input[6] = []              // ch_bbsplit_index
                input[7] = Channel.of(file('https://raw.githubusercontent.com/biocore/sortmerna/v4.3.4/data/rRNA_databases/rfam-5.8s-database-id98.fasta', checkIfExists: true)) // ch_rrna_fastas

                // Skip options
                input[8] = true            // skip_bbsplit
                input[9] = false           // skip_fastqc
                input[10] = false          // skip_trimming
                input[11] = true           // skip_umi_extract
                input[12] = false          // skip_linting

                // Index generation
                input[13] = true           // make_salmon_index
                input[14] = true           // make_sortmerna_index

                // Trimming options
                input[15] = 'trimgalore'   // trimmer
                input[16] = 10             // min_trimmed_reads
                input[17] = true           // save_trimmed
                input[18] = true           // fastp_merge

                // rRNA removal options
                input[19] = true           // remove_ribo_rna
                input[20] = 'sortmerna'    // ribo_removal_tool

                // UMI options
                input[21] = false          // with_umi
                input[22] = 0              // umi_discard_read

                // Strandedness thresholds
                input[23] = 0.8            // stranded_threshold
                input[24] = 0.1            // unstranded_threshold
                """
            }
        }

        then {
            def pelines1 = path(workflow.out.reads[0][1][0]).linesGzip
            def pelines2 = path(workflow.out.reads[0][1][1]).linesGzip

            // First part of each fq lint report line is a timestamp, remove it before snapshotting
            def processed_ribo_removal_lint_report = path(workflow.out.lint_log.find { entry -> entry[1].contains('fq_lint_after_ribo_removal')}?.getAt(1))
                .getText()
                .readLines()
                .collect { line -> line.split(' ', 2)[1] } // Split by the first space and take everything after it
                .join('\n') // Join the processed lines back into a single text block

            // Parse sortmerna log for rRNA detection stats
            def sortmernaLog = path(workflow.out.multiqc_files.find { it.toString().endsWith('.sortmerna.log') })
                .getText()
            def sortmernaRrnaMatch = (sortmernaLog =~ /Total reads passing E-value threshold = (\d+)/)
            def sortmernaRrnaCount = sortmernaRrnaMatch ? sortmernaRrnaMatch[0][1].toInteger() : -1

            // Input: 4159 original + 10 synthetic rRNA = 4169 read pairs
            // After trimgalore: 4169 pairs (no merging, unlike fastp)
            // SortMeRNA removes all 10 synthetic rRNA pairs (20 individual reads), leaving 4159 pairs
            assertAll(
                { assert workflow.success },
                { assert pelines1.size() == 16636 },  // 4159 pairs × 4 lines/read
                { assert pelines2.size() == 16636 },
                { assert workflow.out.trim_read_count[0][1] == 4169.0 },
                { assert sortmernaRrnaCount == 20 },  // 10 pairs = 20 individual reads (100% detection)
                { assert snapshot(
                    pelines1.join('\n').md5(),
                    pelines2.join('\n').md5(),
                    processed_ribo_removal_lint_report.md5()
                ).match() }
            )
        }
    }

}
