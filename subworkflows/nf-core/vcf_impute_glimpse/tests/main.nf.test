nextflow_workflow {

    name "Test Workflow VCF_IMPUTE_GLIMPSE"
    script "../main.nf"
    workflow "VCF_IMPUTE_GLIMPSE"

    tag "subworkflows"
    tag "bcftools/index"
    tag "subworkflows_nfcore"
    tag "subworkflows/vcf_impute_glimpse"
    tag "glimpse/phase"
    tag "glimpse/ligate"
    tag "glimpse/chunk"

    test("Should run without failures") {
        config "./nextflow.config"

        when {
            params {
                outdir = "tests/results"
            }
            workflow {
                """
                samples_infos = Channel.of('NA12878 2').collectFile(name: 'sampleinfos.txt')

                ch_panel = Channel.fromList([
                    [[ chr:"chr21"],
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/popgen/1000GP.chr21.vcf.gz", checkIfExists:true),
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/popgen/1000GP.chr21.vcf.gz.csi", checkIfExists:true)],
                    [[ chr:"chr22"],
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/popgen/1000GP.chr22.vcf.gz", checkIfExists:true),
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/popgen/1000GP.chr22.vcf.gz.csi", checkIfExists:true)]
                ])
                region = Channel.fromList([
                    [[chr: "chr21", region: "chr21:16600000-16800000"], "chr21:16600000-16800000"],
                    [[chr: "chr22", region: "chr22:16600000-16800000"], "chr22:16600000-16800000"]
                ])

                input_vcf = Channel.fromList([
                    [[ id:'NA12878'], // meta map
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878.chr21_22.1X.glimpse2.vcf.gz", checkIfExists: true),
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878.chr21_22.1X.glimpse2.vcf.gz.csi", checkIfExists: true),
                    ],
                    [[ id:'NA19401'], // meta map
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA19401.chr21_22.1X.glimpse2.vcf.gz", checkIfExists: true),
                    file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA19401.chr21_22.1X.glimpse2.vcf.gz.csi", checkIfExists: true),
                    ]
                ])
                input_vcf_multiple = input_vcf
                    .combine( samples_infos )
                    .combine( region )
                    .map{ metaI, vcf, index, sample, metaCR, region ->
                        [metaI + metaCR, vcf, index, sample, region ]
                    }

                ch_map = Channel.fromList([
                    [[ chr: "chr21"],
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/genome/genetic_map/genome.GRCh38.chr21.glimpse.map", checkIfExists: true)
                    ],
                    [[ chr: "chr22"],
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/genome/genetic_map/genome.GRCh38.chr22.glimpse.map", checkIfExists: true)
                    ]
                ])

                // Combine input and map depending on chromosome name
                input[0] = input_vcf_multiple
                    .map{ metaIRC, vcf, index, sample, region ->
                        [metaIRC.subMap(["chr"]), metaIRC, vcf, index, sample, region]
                    }
                    .combine(ch_panel, by: 0)
                    .combine(ch_map, by: 0)
                    .map{ metaC, metaIRC, vcf, index, sample, region, ref, ref_index, map ->
                        [metaIRC, vcf, index, sample, region, ref, ref_index, map]
                    }
                """
            }
        }

        then {
            assertAll(
                { assert workflow.success },
                { assert snapshot(
                    workflow.out.versions,
                    workflow.out.chunk_chr,
                    workflow.out.merged_variants.collect{ meta, vcf -> [
                        meta,
                        file(vcf).name,
                        path(vcf).vcf.summary,
                        path(vcf).vcf.variantsMD5
                    ]}
                ).match() }
            )
        }

    }

}
