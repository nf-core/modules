process TCOFFEE_IRMSD {
    tag "$meta.id"
    label 'process_medium'

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/mulled-v2-a76a981c07359a31ff55b9dc13bd3da5ce1909c1:84c8f17f1259b49e2f7783b95b7a89c6f2cb199e-0':
        'biocontainers/mulled-v2-a76a981c07359a31ff55b9dc13bd3da5ce1909c1:84c8f17f1259b49e2f7783b95b7a89c6f2cb199e-0' }"

    input:
    tuple  val(meta),  path (msa)
    tuple  val(meta2), path(template), path(structures)

    output:
    tuple val(meta), path ("${prefix}.irmsd"), emit: irmsd
    tuple val("${task.process}"), val('tcoffee'), eval('t_coffee -version | awk -F"[ _.]" -v OFS="." \'{print \\$4,\\$5,\\$6}\''), emit: versions_tcoffee, topic: versions
    tuple val("${task.process}"), val('pigz'), eval('pigz --version 2>&1 | sed "s/^.*pigz[[:space:]]*//"'), emit: versions_pigz, topic: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    prefix = task.ext.prefix ?: "${msa.baseName}"
    """
    export TEMP='./'

    if [[ \$(basename $msa) == *.gz ]] ; then
        unpigz -f $msa
    fi

    t_coffee -other_pg irmsd \
        \$(basename $msa .gz) \
        $args \
        -template_file $template > ${prefix}.irmsd
    """

    stub:
    def args = task.ext.args ?: ''
    prefix = task.ext.prefix ?: "${msa.baseName}"
    """
    # Otherwise, tcoffee will crash when calling its version
    export TEMP='./'
    touch ${prefix}.irmsd
    """
}
