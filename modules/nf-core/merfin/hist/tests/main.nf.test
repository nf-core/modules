// nf-core modules test merfin/hist
nextflow_process {

    name "Test Process MERFIN_HIST"
    script "../main.nf"
    process "MERFIN_HIST"

    tag "modules"
    tag "modules_nfcore"
    tag "merfin"
    tag "merfin/hist"


    test("sarscov2 - paired-end illumina reads") {

        setup {

            run("SEQTK_MERGEPE") {
                script "../../../seqtk/mergepe/main.nf"
                tag    "seqtk/mergepe"
                process {
                    """
                    input[0] = [
                        [ id:'test', single_end:false ], // meta map
                        [
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                        ]
                    ]
                    """
                }
            }

            run("MERYL_COUNT") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = SEQTK_MERGEPE.out.reads
                    input[1] = Channel.value(6)
                    """
                }
            }

        }

        when {
            process {
                """
                input[0] = [
                    [ id:'test', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[1] = MERYL_COUNT.out.meryl_db
                input[2] = []
                input[3] = []
                input[4] = 10
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.hist, process.out.versions).match() },
                // The following asserts that the instable output stdout_log generated by the module is complete
                { assert path(process.out.log_stderr.get(0).get(1)).readLines().last().contains("Bye!") }
            )
        }

    }

    test("sarscov2 - paired-end illumina reads - stub") {

        options "-stub"

        setup {

            run("SEQTK_MERGEPE") {
                script "../../../seqtk/mergepe/main.nf"
                tag    "seqtk/mergepe"
                process {
                    """
                    input[0] = [
                        [ id:'test', single_end:false ], // meta map
                        [
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                        ]
                    ]
                    """
                }
            }

            run("MERYL_COUNT") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = SEQTK_MERGEPE.out.reads
                    input[1] = Channel.value(6)
                    """
                }
            }

        }

        when {
            process {
                """
                input[0] = [
                    [ id:'test', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[1] = MERYL_COUNT.out.meryl_db
                input[2] = []
                input[3] = []
                input[4] = 10
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }

    }

    test("sarscov2 - paired-end illumina reads plus optional inputs lookup_table and seqmers") {

        setup {

            run("SEQTK_MERGEPE") {
                script "../../../seqtk/mergepe/main.nf"
                tag    "seqtk/mergepe"
                process {
                    """
                    input[0] = [
                        [ id:'test', single_end:false ], // meta map
                        [
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                        ]
                    ]
                    """
                }
            }

            run("MERYL_COUNT", alias: "MERYL_COUNT_READS") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = SEQTK_MERGEPE.out.reads
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("MERYL_COUNT", alias: "MERYL_COUNT_ASM") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = [
                        [ id:'test', single_end:false ], // meta map
                        file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                    ]
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("MERYL_HISTOGRAM") {
                script "../../../meryl/histogram/main.nf"
                tag    "meryl/histogram"
                process {
                    """
                    input[0] = MERYL_COUNT_READS.out.meryl_db
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("GENOMESCOPE2") {
                script "../../../genomescope2/main.nf"
                config "./nextflow.config"
                tag    "genomescope2"
                params {
                    genomescope2_args = "--ploidy 1 --kmer_length 6 --fitted_hist"
                }
                process {
                    """
                    input[0] = MERYL_HISTOGRAM.out.hist
                    """
                }
            }

        }

        when {
            process {
                """
                input[0] = [
                    [ id:'test', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[1] = MERYL_COUNT_READS.out.meryl_db
                input[2] = GENOMESCOPE2.out.lookup_table.map{ meta, lkp -> lkp}
                input[3] = MERYL_COUNT_ASM.out.meryl_db.map{ meta, meryl_db -> meryl_db }
                input[4] = 10
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.hist, process.out.versions).match() },
                // The following asserts that the instable output stdout_log generated by the module is complete
                { assert path(process.out.log_stderr.get(0).get(1)).readLines().last().contains("Bye!") }
            )
        }

    }

    test("Multiple samples - sarscov2 + bacteroides_fragilis - paired-end illumina reads") {

        setup {

            run("SEQTK_MERGEPE") {
                script "../../../seqtk/mergepe/main.nf"
                tag    "seqtk/mergepe"
                process {
                    """
                    input[0] = Channel.from([
                        [ id:'test_sars', single_end:false ], // meta map
                        [
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                            file(params.modules_testdata_base_path + '/genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                        ]
                    ], [
                        [ id:'test_fragilis', single_end:false ], // meta map
                        [
                            file(params.modules_testdata_base_path + '/genomics/prokaryotes/bacteroides_fragilis/illumina/fastq/test1_1.fastq.gz', checkIfExists: true),
                            file(params.modules_testdata_base_path + '/genomics/prokaryotes/bacteroides_fragilis/illumina/fastq/test1_2.fastq.gz', checkIfExists: true)
                        ]
                    ])
                    """
                }
            }

            run("MERYL_COUNT", alias: "MERYL_COUNT_READS") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = SEQTK_MERGEPE.out.reads
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("MERYL_COUNT", alias: "MERYL_COUNT_ASM") {
                script "../../../meryl/count/main.nf"
                tag    "meryl/count"
                process {
                    """
                    input[0] = Channel.from([
                        [ id:'test_sars', single_end:false ], // meta map
                        file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                    ],[
                        [ id:'test_fragilis', single_end:false ], // meta map
                        file(params.modules_testdata_base_path + '/genomics/prokaryotes/bacteroides_fragilis/genome/genome.fna.gz', checkIfExists: true)
                    ])
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("MERYL_HISTOGRAM") {
                script "../../../meryl/histogram/main.nf"
                tag    "meryl/histogram"
                process {
                    """
                    input[0] = MERYL_COUNT_READS.out.meryl_db
                    input[1] = Channel.value(6)
                    """
                }
            }

            run("GENOMESCOPE2") {
                script "../../../genomescope2/main.nf"
                config "./nextflow.config"
                tag    "genomescope2"
                params {
                    genomescope2_args = "--ploidy 1 --kmer_length 6 --fitted_hist"
                }
                process {
                    """
                    input[0] = MERYL_HISTOGRAM.out.hist
                    """
                }
            }

        }

        when {
            process {
                """
                Channel.from([
                    [ id:'test_sars', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + '/genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ],[
                    [ id:'test_fragilis', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + '/genomics/prokaryotes/bacteroides_fragilis/genome/genome.fna.gz', checkIfExists: true)
                ])
                .set{ input_zero_ch }

                input_zero_ch.join( MERYL_COUNT_READS.out.meryl_db )
                             .join( MERYL_COUNT_ASM.out.meryl_db )
                             .join( GENOMESCOPE2.out.lookup_table )
                             .multiMap{ meta, asm, meryl_db, asm_meryl_db, lookup_table ->
                    asm_ch: [meta, asm]
                    meryl_ch: [meta, meryl_db]
                    asm_meryl_ch: [asm_meryl_db]
                    lk_ch: [lookup_table]
                }.set{ multimap_inputs_ch }

                input[0] = multimap_inputs_ch.asm_ch
                input[1] = multimap_inputs_ch.meryl_ch
                input[2] = multimap_inputs_ch.lk_ch
                input[3] = multimap_inputs_ch.asm_meryl_ch
                input[4] = 10
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.hist, process.out.versions).match() },
                // The following asserts that the output stdout_log generated by the module is complete
                { assert path(process.out.log_stderr.get(0).get(1)).readLines().last().contains("Bye!") }
            )
        }

    }

}
