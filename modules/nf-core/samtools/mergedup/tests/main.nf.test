nextflow_process {

    name "Test Process SAMTOOLS_MERGEDUP"
    script "../main.nf"
    process "SAMTOOLS_MERGEDUP"

    tag "modules"
    tag "modules_nfcore"
    tag "samtools"
    tag "samtools/mergedup"
    tag "samtools/fixmate"
    tag "samtools/sort"

    config "./nextflow.config"

    setup {
        run("SAMTOOLS_FIXMATE") {
            script "../../fixmate/main.nf"
            process {
                """
                input[0] = Channel.of(
                    [
                        [id: 'test', single_end: false], // meta map
                        file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/bam/test.paired_end.name.sorted.bam', checkIfExists: true)
                    ],
                    [
                        [id: 'test2', single_end: false], // meta map
                        file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/bam/test.paired_end.name.sorted.bam', checkIfExists: true)
                    ]
                )
                """
            }
        }

        run("SAMTOOLS_SORT") {
            script "../../sort/main.nf"
            process {
                """
                input[0] = SAMTOOLS_FIXMATE.out.bam
                input[1] = [[], []]
                input[2] = "csi"
                """
            }
        }
    }

    test("sarscov2 - bam") {

        when {

            params {
                samtools_markdup_args = [
                    "-s",                           // print some stats
                    "--json",                       // output stats in json format for MultiQC
                    "-d 2500",                      // The optical duplicate distance
                    "--barcode-name",               // Use the UMI/barcode embedded in the read name (eigth colon delimited part).
                    "--write-index",                // Write csi/crai index
                ].join(" ").trim()
            }

            process {
                """
                input[0] = SAMTOOLS_SORT.out.bam
                    .map { _meta, bam -> [[id: "test"], bam] }
                    .groupTuple(by: 0)
                input[1] = Channel.of([
                    [ id:'fasta' ], // meta map
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ])
                """
            }

        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }
    }

    test("sarscov2 - cram") {

        when {

            params {
                samtools_markdup_args = [
                    "-s",                           // print some stats
                    "--json",                       // output stats in json format for MultiQC
                    "-d 2500",                      // The optical duplicate distance
                    "--barcode-name",               // Use the UMI/barcode embedded in the read name (eigth colon delimited part).
                    "--write-index",                // Write csi/crai index
                    "--output-fmt cram,version=3.0",// Output format
                    "--output-fmt-option archive"   // Cram compression level
                ].join(" ").trim()
                modules_testdata_base_path = 'https://raw.githubusercontent.com/nf-core/test-datasets/modules/data/'
            }

            process {
                """
                input[0] = SAMTOOLS_SORT.out.bam
                    .map { _meta, bam -> [[id: "test"], bam] }
                    .groupTuple(by: 0)
                input[1] = Channel.of([
                    [ id:'fasta' ], // meta map
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ])
                """
            }
        }

        then {
            def fasta = params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta'
            assertAll(
                { assert process.success },
                {
                    assert snapshot(
                        cram(
                            process.out.cram[0][1],
                            fasta,
                        ).getReadsMD5(),
                        file(process.out.crai[0][1]).name,
                        process.out.metrics,
                    ).match()
                }
            )
        }
    }


    test("sarscov2 - bam - stub") {

        options "-stub"

        when {

            params {
                samtools_markdup_args = ""
            }

            process {
                """
                input[0] = SAMTOOLS_SORT.out.bam
                    .map { _meta, bam -> [[id: "test"], bam] }
                    .groupTuple(by: 0)
                input[1] = Channel.of([
                    [ id:'fasta' ], // meta map
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ])
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }

    }

}
