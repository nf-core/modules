nextflow_process {

    name "Test Process BWA_SAMSE"
    tag "modules_nfcore"
    tag "modules"
    tag "bwa"
    tag "bwa/samse"
    tag "bwa/aln"
    tag "bwa/index"
    script "../main.nf"
    process "BWA_SAMSE"

    test("SamSE") {
        setup {
            run("BWA_INDEX") {
                script "../../index/main.nf"
                process {
                    """
                    input[0] = [
                        [id: 'test'],
                        file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                    ]
                    """
                }
            }
            run("BWA_ALN") {
                script "../../aln/main.nf"
                process {
                    """
                    input[0] = [
                        [ id:'test', single_end:true ],
                        file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true)
                    ]
                    input[1] = BWA_INDEX.out.index
                    """
                }
            }
        }

        when {
            process {
                """
                input[0] = channel.of([[ id:'test', single_end:true],
                            file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true)
                        ])
                    .join(BWA_ALN.out.sai)
                input[1] = BWA_INDEX.out.index
                """
            }
        }

        then {
            assert process.success
            assertAll(
                { assert snapshot(
                    bam(process.out.bam[0][1]).getReadsMD5(),
                    process.out.findAll { key, val -> key.startsWith("versions") }
                    ).match() }
            )
        }
    }

    test("SamSE -- stub") {
        options "-stub"
        when {
            process {
                """
                input[0] = Channel.of([
                    [ id:'test', single_end:false ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                    []
                ])
                input[1] = [[], []]
                """
            }
        }
        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }
    }

}
