nextflow_process {

    name "Test Process SHAPEIT5_PHASERARE"
    script "../main.nf"
    process "SHAPEIT5_PHASERARE"

    tag "modules"
    tag "modules_nfcore"
    tag "shapeit5"
    tag "shapeit5/phaserare"
    tag "shapeit5/phasecommon"
    tag "bcftools/index"

    test("homo sapiens - vcf, scaffold, []") {
        config "./nextflow.config"
        setup {
            run("SHAPEIT5_PHASECOMMON") {
                script "../../../shapeit5/phasecommon"
                params {
                    shapeit5_phasecommon_args = "--filter-maf 0.001"
                }
                process {
                    """
                    input[0] = Channel.of([
                        [ id:'scaffold', single_end:false ], // meta map
                        file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf", checkIfExists: true),
                        file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf.csi", checkIfExists: true),
                        [],
                        "chr21",
                    ])
                    input[1] = Channel.of([[],[],[]])
                    input[2] = Channel.of([[],[],[]])
                    input[3] = Channel.of([[],[]])
                    """
                }
            }
            run("BCFTOOLS_INDEX") {
                script "../../../bcftools/index"
                process {
                    """
                    input[0] = SHAPEIT5_PHASECOMMON.out.phased_variant
                    """
                }
            }
        }

        when {
            process {
                """
                input[0] = Channel.of([
                    [ id:'input', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf", checkIfExists: true),
                    file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf.csi", checkIfExists: true),
                    [],
                    "chr21:16650000-16750000",
                ])
                input[1] = SHAPEIT5_PHASECOMMON.out.phased_variant
                    .join(BCFTOOLS_INDEX.out.csi)
                    .combine(Channel.of("chr21:16600000-16800000"))
                input[2] = Channel.of([[],[]])
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(
                    process.out.phased_variant.collect { meta, vcf -> [ meta, file(vcf).name ] },
                    process.out.versions
                ).match() }
            )
        }

    }

    test("homo sapiens - vcf, scaffold, [] - stub") {
        options "-stub"

        when {
            process {
                """
                input[0] = Channel.of([
                    [ id:'input', single_end:false ], // meta map
                    file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf", checkIfExists: true),
                    file(params.modules_testdata_base_path + "delete_me/glimpse/1000GP.chr21.noNA12878.s.bcf.csi", checkIfExists: true),
                    [],
                    "chr21:16600000-16800000",
                ])
                input[1] = Channel.of([[],[],[],[]])
                input[2] = Channel.of([[],[]])
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }

    }

}
