nextflow_process {

    name "Test Process SHAPEIT5_LIGATE"
    script "../main.nf"
    process "SHAPEIT5_LIGATE"

    tag "modules"
    tag "modules_nfcore"
    tag "shapeit5"
    tag "shapeit5/ligate"
    tag "bcftools/view"

    test("homo sapiens - map, [vcf], [tbi]") {
        config "./nextflow.config"
        setup {
            run("BCFTOOLS_VIEW", alias: "BCFTOOLS_VIEW_1") {
                script "../../../bcftools/view"
                params {
                    bcftools_args = "--regions chr22:16570000-16600000 -Ob --write-index=csi -e 'GT=\"./.\"||GT=\".\"'"
                }
                process {
                    """
                    input[0] = [
                        [ id:'NA12878_1' ], // meta map
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz", checkIfExists: true),
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz.csi", checkIfExists: true),
                    ]
                    input[1] = []
                    input[2] = []
                    input[3] = []
                    """
                }
            }
            run("BCFTOOLS_VIEW", alias: "BCFTOOLS_VIEW_2") {
                script "../../../bcftools/view"
                params {
                    bcftools_args = "--regions chr22:16580000-16610000 -Ob --write-index=csi -e 'GT=\"./.\"||GT=\".\"'"
                }
                process {
                    """
                    input[0] = [
                        [ id:'NA12878_2' ], // meta map
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz", checkIfExists: true),
                        file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz.csi", checkIfExists: true),
                    ]
                    input[1] = []
                    input[2] = []
                    input[3] = []
                    """
                }
            }
        }

        when {
            process {
                """
                input[0] = BCFTOOLS_VIEW_1.out.vcf.join(BCFTOOLS_VIEW_1.out.csi)
                    .mix(BCFTOOLS_VIEW_2.out.vcf.join(BCFTOOLS_VIEW_2.out.csi))
                    .map { meta, vcf, csi -> [ [id : "NA12878"], vcf, csi ] }
                    .groupTuple()
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(
                    process.out.merged_variants.collect {meta, vcf -> [ meta, path(vcf).vcf.summary ]},
                    process.out.versions
                ).match() }
            )
        }
    }

    test("homo sapiens - map, [vcf], [tbi] - stub") {
        options '-stub'

        when {
            process {
                """
                input[0] = [
                    [id: 'NA12878'],
                    [file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz", checkIfExists: true)],
                    [file(params.modules_testdata_base_path + "genomics/homo_sapiens/illumina/vcf/NA12878_GIAB.chr22.vcf.gz.csi", checkIfExists: true)]
                ]
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }
    }

}
