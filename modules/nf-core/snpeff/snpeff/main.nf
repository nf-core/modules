process SNPEFF_SNPEFF {
    tag "${meta.id}"
    label 'process_medium'

    conda "${moduleDir}/environment.yml"
    container "${workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container
        ? 'https://community-cr-prod.seqera.io/docker/registry/v2/blobs/sha256/30/30669e5208952f30d59d0d559928772f082830d01a140a853fff13a2283a17b0/data'
        : 'community.wave.seqera.io/library/snpeff:5.4.0a--eaf6ce30125b2b17'}"

    input:
    tuple val(meta), path(vcf)
    tuple val(meta_db), path(snpeff_db)
    tuple val(meta_config), path(snpeff_config)
    // tuple val(meta_db), path(snpeff_db) // TODO alternative option: provide the entire snpeff_db folder from SNPEFF_BUILD
    val db_name                         // Optional name of the snpEff genome database, defaults to meta_db.id
    // TODO optional db-path? as value/string to make it relative to snpEff.config or absolute path?
    // TODO: add interval filter -fi , -filterInterval  <file>   : Only analyze changes that intersect with the intervals specified in this file (you may use this option many times)

    output:
    tuple val(meta), path("*.ann.vcf")    , emit: vcf
    tuple val(meta), path("*.csv")        , emit: report
    tuple val(meta), path("*.html")       , emit: summary
    tuple val(meta), path("*.genes.txt")  , emit: genes
    tuple val("${task.process}"), val('snpeff'), eval("snpEff -version 2>&1 | cut -f 2 -d '\t'"), topic: versions, emit: versions_snpeff

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def avail_mem = 6144
    if (!task.memory) {
        log.info('[snpEff] Available memory not known - defaulting to 6GB. Specify process memory requirements to change this.')
    }
    else {
        avail_mem = (task.memory.mega * 0.8).intValue()
    }
    def prefix = task.ext.prefix ?: "${meta.id}"
    // set db_name to meta_db.id of snpeff_db folder when not supplied
    db_name = db_name ?: meta_db.id
    // ensure snpeff_db and snpeff_config match
    if( meta_config?.id && meta_db?.id && meta_config.id != meta_db.id ) {
        error "[snpEff] snpEff database metadata id '${meta_db.id}' does not match config metadata id '${meta_config.id}'."
    }
    """
    # The snpeff_db folder contains:
    #   snpEff.config (generated by BUILD)
    #   data/${db_name}/ (the actual database)

    # NOTE: dataDir is relative to snpEff.config when provided as a relative path. Depends on whether or not self-contained database dir + config is passed as input (relative path) or if they are provided separately (absolute path).
    snpEff ann \\
        -Xmx${avail_mem}M \\
        -XX:-UsePerfData \\
        -config ${snpeff_config} \\
        -dataDir ${snpeff_db} \\
        -csvStats ${prefix}.csv \\
        -stats ${prefix}.html \\
        ${args} \\
        ${db_name} \\
        ${vcf} \\
        > ${prefix}.ann.vcf
    """

    stub:
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    touch ${prefix}.ann.vcf
    touch ${prefix}.csv
    touch ${prefix}.html
    touch ${prefix}.genes.txt
    """
}
