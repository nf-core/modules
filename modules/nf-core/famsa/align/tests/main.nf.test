nextflow_process {

    name "Test Process FAMSA_ALIGN"
    script "../main.nf"
    process "FAMSA_ALIGN"

    tag "modules"
    tag "modules_nfcore"
    tag "famsa"
    tag "famsa/align"
    tag "famsa/guidetree"

    test("sarscov2 - fasta - uncompressed") {

        when {
            process {
                """
                input[0] = [ [ id:'test' ], // meta map
                             file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                           ]
                input[1] = [[:],[]]
                input[2] = false
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.alignment).match("alignment_uncompressed")},
                { assert snapshot(process.out.versions).match("versions0") }
            )
        }

    }

    test("sarscov2 - fasta - compressed") {

        when {
            process {
                """
                input[0] = [ [ id:'test' ], // meta map
                             file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                           ]
                input[1] = [[:],[]]
                input[2] = true
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.alignment).match("alignment_compressed")},
                { assert snapshot(process.out.versions).match("versions1") }
            )
        }

    }

    test("sarscov2 - fasta - guide_tree") {

        setup {
            run("FAMSA_GUIDETREE") {
                script "../../guidetree/main.nf"
                process {
                    """
                    input[0] = [ [ id:'test' ], // meta map
                                 file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                               ]
                    """
                }
            }
        }

        when {
            process {
                """
                input[0] = [ [ id:'test' ], // meta map
                             file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                           ]
                input[1] = FAMSA_GUIDETREE.out.tree.collect{ meta, tree -> tree }.map{ tree -> [[ id: 'test_summary'], tree]}
                input[2] = true
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out.alignment).match("with_guide_tree_alignment")},
                { assert snapshot(process.out.versions).match("with_guide_tree_versions") }
            )
        }
    }
}