nextflow_process {

    name "Test Process BISMARK_REPORT"
    script "../main.nf"
    process "BISMARK_REPORT"

    tag "modules"
    tag "modules_nfcore"
    tag "bismark"
    tag "bismark/report"
    tag "bismark/align"
    tag "bismark/deduplicate"
    tag "bismark/methylationextractor"
    tag "bismark/genomepreparation"

    test("sarscov2 genome [fasta] | sarscov2 single-end methylated [fastq]") {

        setup {
            run("BISMARK_GENOMEPREPARATION") {
                script "../../genomepreparation/main.nf"
                process {
                    """
                    input[0] = Channel.of([
                        [ id:'test' ], // meta map
                        file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                    ])
                    """
                }
            }

            run("BISMARK_ALIGN") {
                script "../../align/main.nf"
                process {
                    """
                    input[0] = Channel.of([
                                [ id:'test', single_end:true ],
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test.methylated_1.fastq.gz', checkIfExists: true)
                    ])
                    input[1] = Channel.of([ [ id:'sarscov2'], file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true) ])
                    input[2] = BISMARK_GENOMEPREPARATION.out.index
                    """
                }
            }

            run("BISMARK_DEDUPLICATE") {
                script "../../deduplicate/main.nf"
                process {
                    """
                    input[0] = BISMARK_ALIGN.out.bam
                    """
                }
            }

            run("BISMARK_METHYLATIONEXTRACTOR") {
                script "../../methylationextractor/main.nf"
                process {
                    """
                    input[0] = BISMARK_ALIGN.out.bam
                    input[1] = BISMARK_GENOMEPREPARATION.out.index
                    """
                }
            }
        }

        when {
            process {
                """
                input[0] = BISMARK_ALIGN.out.report
                            .join(BISMARK_DEDUPLICATE.out.report)
                            .join(BISMARK_METHYLATIONEXTRACTOR.out.report)
                            .join(BISMARK_METHYLATIONEXTRACTOR.out.mbias)
                """
            }
        }

        then {
            assertAll(
            { assert process.success },
            { assert snapshot(
                file(process.out.report[0][1]).name,
                process.out.versions
                ).match() },
            )
        }

    }

}
