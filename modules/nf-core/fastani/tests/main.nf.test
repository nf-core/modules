nextflow_process {

    name "Test Process FASTANI"
    script "../main.nf"
    process "FASTANI"
    config "./nextflow.config"
    tag "modules_nfcore"
    tag "modules"
    tag "fastani"

    test("sarscov2 - referece vs contigs - fastANI - 1 v 1 mode") {

        when {
            process {
                """
                input[0] = [
                    [ id:'test' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[1] = [
                    [ id:'test2' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                ]
                input[2] = []
                input[3] = []
                """
            }
        }

        then {
            assertAll (
                { assert process.success },
                { assert snapshot(
                    process.out.ani,
                    process.out.findAll { key, val -> key.startsWith("versions")}
                ).match() }
            )
        }
    }

    test("sarscov2 - referece vs contigs - fastANI - all vs all mode") {

        when {
            process {
                """
                def q_src = file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                def r_src = file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)

                // stage into the nf-test execution directory
                def q_stage = new File(workDir.toString(), 'query.fasta')
                def r_stage = new File(workDir.toString(), 'ref.fasta')

                // Ensures files are staged locally
                q_stage.bytes = q_src.bytes
                r_stage.bytes = r_src.bytes

                // create ql/rl list files
                def ql_file = new File(workDir.toString(), 'query.list')
                def rl_file = new File(workDir.toString(), 'reference.list')

                ql_file.text =
                    q_stage.getAbsolutePath() + System.lineSeparator() +
                    r_stage.getAbsolutePath() + System.lineSeparator()

                rl_file.text =
                    q_stage.getAbsolutePath() + System.lineSeparator() +
                    r_stage.getAbsolutePath() + System.lineSeparator()

                input[0] = [ [], [] ]
                input[1] = [ [], [] ]
                input[2] = file( ql_file.getAbsolutePath(), checkIfExists: true )
                input[3] = file( rl_file.getAbsolutePath(), checkIfExists: true )
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                {
                    def aniFile = path(process.out.ani[0][1])
                    assert aniFile.exists()
                    def lines = aniFile.readLines()
                    assert lines.size() == 4 : "Expected 4 ANI comparisons but found ${lines.size()}"
                },
                { assert snapshot(process.out.findAll { key, val -> key.startsWith("versions") }).match() }
            )
        }
    }

    test("stub") {

        options '-stub'

        when {
            process {
                """
                input[0] = [
                    [ id:'test' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[1] = [
                    [ id:'test2' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fasta/contigs.fasta', checkIfExists: true)
                ]
                input[2] = []
                input[3] = []
                """
            }
        }

        then {
            assertAll (
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }

    }

}
