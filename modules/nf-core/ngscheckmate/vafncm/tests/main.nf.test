nextflow_process {

    name "Test Process NGSCHECKMATE_VAFNCM"
    script "../main.nf"
    config "./nextflow.config"
    process "NGSCHECKMATE_VAFNCM"

    tag "modules"
    tag "modules_nfcore"
    tag "ngscheckmate"
    tag "ngscheckmate/vafncm"
    tag "gawk"
    tag "bedtools/makewindows"
    tag "bowtie/build"
    tag "ngscheckmate/patterngenerator"
    tag "ngscheckmate/fastq"

    setup {

        run("GAWK" , alias: "GAWK_FAI") {
            script "../../../gawk/main.nf"
            process {
                """
                input[0] = [
                    [ id: 'sarscov2' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta.fai', checkIfExists: true)
                    ]
                input[1] = []
                """
            }
        }

        run("BEDTOOLS_MAKEWINDOWS") {
            script "../../../bedtools/makewindows/main.nf"
            process {
                """
                input[0] = GAWK_FAI.out.output
                """
            }
        }

        run("GAWK" , alias: "GAWK_BED") {
            script "../../../gawk/main.nf"
            process {
                """
                input[0] = BEDTOOLS_MAKEWINDOWS.out.bed
                input[1] = []
                """
            }
        }

        run("BOWTIE_BUILD") {
            script "../../../bowtie/build/main.nf"
            process {
                """
                input[0] = [
                    [ id: 'sarscov2' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                """
            }
        }

        run("NGSCHECKMATE_PATTERNGENERATOR") {
            script "../../patterngenerator/main.nf"
            process {
                """
                input[0] = GAWK_BED.out.output
                input[1] = [
                    [ id: 'sarscov2' ],
                    file(params.modules_testdata_base_path + 'genomics/sarscov2/genome/genome.fasta', checkIfExists: true)
                ]
                input[2] = BOWTIE_BUILD.out.index
                """
            }
        }

        run("NGSCHECKMATE_FASTQ") {
            script "../../fastq/main.nf"
            process {
                """
                input[0] = channel.fromList(
                    [
                        [
                            [ id:'test1', single_end:false ], // meta map
                            [
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                            ]
                        ],
                        [
                            [ id:'test2', single_end:false ], // meta map
                            [
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_1.fastq.gz', checkIfExists: true),
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test_2.fastq.gz', checkIfExists: true)
                            ]
                        ],
                        [
                            [ id:'test3', single_end:false ], // meta map
                            [
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test.methylated_1.fastq.gz', checkIfExists: true),
                                file(params.modules_testdata_base_path + 'genomics/sarscov2/illumina/fastq/test.methylated_2.fastq.gz', checkIfExists: true)
                            ]
                        ],
                    ]
                )
                input[1] = NGSCHECKMATE_PATTERNGENERATOR.out.pt
                """
            }
        }

    }

    test("sarscov2 - fastq") {

        when {
            process {
                """
                input[0] = NGSCHECKMATE_FASTQ.out.vaf.map{it[1]}.collect().map{files -> [[id:"combined"], files]}
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(
                    process.out.corr_matrix,
                    process.out.matched,
                    process.out.all,
                    process.out.versions).match() }
            )
        }

    }

    test("sarscov2 - fastq - stub") {

        options "-stub"

        when {
            process {
                """
                input[0] = NGSCHECKMATE_FASTQ.out.vaf.map{it[1]}.collect().map{files -> [[id:"combined"], files]}
                """
            }
        }

        then {
            assertAll(
                { assert process.success },
                { assert snapshot(process.out).match() }
            )
        }

    }

}
