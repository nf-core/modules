process TRUVARI_SEGMENT {
    tag "$meta.id"
    label 'process_single'

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://depot.galaxyproject.org/singularity/truvari:5.4.0--pyhdfd78af_0':
        'biocontainers/truvari:5.4.0--pyhdfd78af_0' }"

    input:
    tuple val(meta), path(vcf)

    output:
    tuple val(meta), path("*.vcf"), emit: vcf
    tuple val("${task.process}"), val('truvari'), eval("truvari version 2>&1 | sed 's/Truvari v//'"), topic: versions, emit: versions_truvari

    when:
    task.ext.when == null || task.ext.when

    script:
    def args = task.ext.args ?: ''
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    truvari \\
        segment \\
        -o ${prefix}.vcf \\
        $args \\
        $vcf

    """

    stub:
    def prefix = task.ext.prefix ?: "${meta.id}"
    """
    touch ${prefix}.vcf

    """
}
